/*---------------------------------------------------------------------------*\
  *      .  *_______ * ______ .  __ *  __ * ___ .___    .  ___ .   *  .     *
    *  .    /       | |   _  \  |  |  |  | |   \/   | *   /   \ *   .    *   .
 *    .  * .\   (---*.|  |_)  |.|  |  |  |*|  \  /  |. * /  *  \  .  *     *
 =^^=^^==^^^=\   \^=^=|   ___/=^|  |^=|  |=|  |\/|  |^^=/  /=\  \^=^=^^===^^^=
 0  o  O  o---)   \ 0 |  |   0  |  o--o  |o|  |  |  | o/  _____  \ 0   o  O
     0    |_______/   |__| o   o \______/  |__| 0|__| /__/  o  \__\   o
  O   o  o        0  o      0   O        o    o       O  o     0   o    0  o
-------------------------------------------------------------------------------
    Copyright (C) 2025 Cineca
-------------------------------------------------------------------------------
License
    This file is part of SPUMA.

    SPUMA is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    SPUMA is distributed in the hope that it will be useful, but
    WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
    or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
    for more details.

    You should have received a copy of the GNU General Public License
    along with SPUMA.  If not, see <http://www.gnu.org/licenses/>.

\*---------------------------------------------------------------------------*/

#include <cuda_runtime_api.h>
#include "cudaMemoryExecutor.cuh"
#include "memoryKernels.H"
#include "error.H"
#include "cudaError.cuh"
#include "cudaDeviceInit.cuh"
#include "deviceUtils.H"

// * * * * * * * * * * * * Public Member Functions * * * * * * * * * * * * * //

void* Foam::cudaMemoryExecutor::_backendAlloc(uint64_t size)
{

    void* ptr;
    label err = CHECK_CUDA_ERROR(cudaMallocManaged((void**)&ptr, size));

    if (err != 0)
    {
        FatalErrorInFunction << "ERROR: cudaMallocManaged returned " << err << abort(FatalError);
    }

    // GPU-residency hints. Where managed memory can migrate to the device
    // (concurrentManagedAccess==1, e.g. native-Linux V100/A100), advise the
    // driver to keep these pages resident on the GPU and prefetch them, so
    // kernels read device memory instead of fault-migrating (or, on platforms
    // without migration, accessing host-pinned memory over PCIe) on every
    // access. This is a no-op where unsupported (e.g. WSL2,
    // concurrentManagedAccess==0) so it never errors there.
    static int s_dev = -1;
    static int s_canMigrate = -1;
    if (s_canMigrate < 0)
    {
        cudaGetDevice(&s_dev);
        cudaDeviceGetAttribute
        (
            &s_canMigrate, cudaDevAttrConcurrentManagedAccess, s_dev
        );
    }
    if (s_canMigrate > 0 && ptr != nullptr && size > 0)
    {
        cudaMemLocation loc;
        loc.type = cudaMemLocationTypeDevice;
        loc.id   = s_dev;
        cudaMemAdvise(ptr, size, cudaMemAdviseSetPreferredLocation, loc);
        cudaMemAdvise(ptr, size, cudaMemAdviseSetAccessedBy, loc);
        cudaMemPrefetchAsync(ptr, size, loc, 0, 0);
    }

    return ptr;
}

void Foam::cudaMemoryExecutor::_backendClear(void* ptr)
{
    if (ptr)
    {
        label err = CHECK_CUDA_ERROR(cudaFree(ptr));
        if (err != 0)
        {
            FatalErrorInFunction << "ERROR: cudaFree returned " << err << abort(FatalError);
        }
    }
}

void Foam::cudaMemoryExecutor::_backendMemCopy
(
   void* dst,
   const void* src,
   uint64_t size,
   memCopyKind kind
)
{
    label err = 0;
    if (kind == memCopyKind::memCopyHostToDevice)
        err = CHECK_CUDA_ERROR(cudaMemcpy(dst, src, (size_t) size, cudaMemcpyHostToDevice));
    else if (kind == memCopyKind::memCopyDeviceToHost)
        err = CHECK_CUDA_ERROR(cudaMemcpy(dst, src, (size_t) size, cudaMemcpyDeviceToHost));
    else if (kind == memCopyKind::memCopyDeviceToDevice)
        err = CHECK_CUDA_ERROR(cudaMemcpy(dst, src, (size_t) size, cudaMemcpyDeviceToDevice));
    else if (kind == memCopyKind::memCopyDefault)
        err = CHECK_CUDA_ERROR(cudaMemcpy(dst, src, (size_t) size, cudaMemcpyDefault));
    else
        FatalErrorInFunction << "ERROR: memCopyKind not found" << abort(FatalError);

    if (err != 0)
        FatalErrorInFunction << "ERROR: cudaMemcpy returned " << err << abort(FatalError);
}

void Foam::cudaMemoryExecutor::_backendMemSet
(
    void* ptr,
    const uint64_t sizeInBytes,
    const void* value,
    size_t sizeOfValue
)
{
    label err = CHECK_CUDA_ERROR
    (
        cudaMemcpyAsync
        (
            ptr,
            value,
            sizeOfValue,
            cudaMemcpyHostToDevice
        )
    );
    if (err != 0)
        FatalErrorInFunction << "ERROR: cudaMemcpy returned " << err << abort(FatalError);

    const int nThreadsPerBlock = cudaDeviceInit::getNumberOfThreadsPerBlock();
    size_t numBlocks = device::setNumBlocks(sizeInBytes - sizeOfValue, nThreadsPerBlock);
    numBlocks = numBlocks == 0 ? 1 : numBlocks;
    Foam::device::memSetKernel<<<numBlocks, nThreadsPerBlock>>>
    (
        sizeInBytes - sizeOfValue,
        (int) sizeOfValue,
        (char*)ptr + sizeOfValue,
        (const char*) ptr
    );
    
    cudaDeviceSynchronize();
    CHECK_LAST_CUDA_ERROR();
}

void Foam::cudaMemoryExecutor::_backendMemSetScalarOne
(
    void* ptr,
    const uint64_t sizeInBytes
)
{
    const int nThreadsPerBlock = cudaDeviceInit::getNumberOfThreadsPerBlock();
    const int numBlocks = device::setNumBlocks(sizeInBytes, nThreadsPerBlock);
    Foam::device::memSetOneKernel<<<numBlocks, nThreadsPerBlock>>>
    (
        sizeInBytes/sizeof(scalar),
        (scalar*)ptr
    );
    
    cudaDeviceSynchronize();
    CHECK_LAST_CUDA_ERROR();
}

void Foam::cudaMemoryExecutor::_backendMemSet
(
    void* ptr,
    const uint64_t sizeInBytes,
    const int value
)
{
    label err = CHECK_CUDA_ERROR
    (
        cudaMemset
        (
            ptr,
            value,
            sizeInBytes
        )
    );

    if (err != 0)
        FatalErrorInFunction << "ERROR: cudaMemcpy returned " << err << abort(FatalError);
}

// ************************************************************************* //
